@muladd begin
#! format: noindent

# tabulate e(T) and c_v(T) of a single species on `T_grid`, adding the translational
# contributions the caller-supplied `f` is required to leave out
# f takes in the species' mass and a temperature and returns the
# internal energy and specific heat of the internal degrees of freedom
function tabulate_species!(e_col, c_v_col, m, f, T_grid)
    @inbounds for i in eachindex(T_grid)
        T = T_grid[i]
        e_int, c_int = f(m, T)
        e_col[i] = (3.0 / 2.0) * k_B * T / m + e_int
        c_v_col[i] = (3.0 / 2.0) * k_B / m + c_int
    end
    return nothing
end

# tabulate e(T) and c_v(T) of every species on `T_grid`, one sweep per species
# both tables are length(T_grid) * NCOMP; callers that only need one of them
# (the c_v discretization grid is not the energy grid for `CvOffset`) discard the other
function tabulate_e_c(mass_arr, e_c_int_function_arr, T_grid, NCOMP)
    n = length(T_grid)
    e_arr = zeros(n, NCOMP)
    c_v_arr = zeros(n, NCOMP)

    for j in 1:NCOMP
        tabulate_species!(view(e_arr, :, j), view(c_v_arr, :, j),
                          mass_arr[j], e_c_int_function_arr[j], T_grid)
    end

    return e_arr, c_v_arr
end

# accumulate ∫ c_v / T dT over the points of the c_v grid, c_v being linear in each
# cell; the partial cell beyond the last grid point is added by
# `entropy_c_v_integral_component`
function tabulate_int_c_v_over_t(c_v_arr, T_c_grid, dT, NCOMP)
    n = length(T_c_grid)
    int_c_v_over_t_arr = zeros(n, NCOMP)

    @inbounds for j in 1:NCOMP
        for i in 2:n
            T_a, T_b = T_c_grid[i - 1], T_c_grid[i]
            c_v_a, c_v_b = c_v_arr[i - 1, j], c_v_arr[i, j]

            int_c_v_over_t_arr[i, j] = int_c_v_over_t_arr[i - 1, j] +
                                       (c_v_a - (c_v_b - c_v_a) * T_a / dT) *
                                       log(T_b / T_a) + (c_v_b - c_v_a)
        end
    end

    return int_c_v_over_t_arr
end

@doc raw"""
    ThermoData1T(ref_q, mass_arr, e_c_int_function_arr;
                 T_min = 10.0, T_max = 3.0e4, T_tol = 1e-9, dT = 1.0,
                 interpolation = :linear, cv_table_offset = false)

Tabulated thermodynamic data for a flow of `NCOMP` species in thermal equilibrium,
i.e. a single temperature ``T`` shared by all degrees of freedom.

For each species ``i`` the specific internal energy and the specific heat at constant
volume are split into a translational part, which is added internally, and a part
carrying the internal degrees of freedom, which is supplied by the caller:
```math
e_i(T) = \frac{3}{2} \frac{k_B T}{m_i} + e_i^{\text{int}}(T),
\qquad
c_{v,i}(T) = \frac{3}{2} \frac{k_B}{m_i} + c_{v,i}^{\text{int}}(T)
```
Both are sampled on a uniform grid of step ``\Delta T`` and evaluated by linear
interpolation. The entropy integral
```math
\int_{T_0}^{T} \frac{c_{v,i}(\tau)}{\tau} \, \mathrm{d}\tau
```
is accumulated over the tabulation points and completed with an exact partial-cell
contribution, ``T_0`` being the first point of the ``c_v`` grid.

The energy table holds ``N_T`` points at ``T_{\min} + (i-1)\Delta T``. Where the
``c_v`` table lives is set by `cv_table_offset`:

- `false` ([`NoCvOffset`](@ref)): the same grid as the energy table.
- `true` ([`CvOffset`](@ref)): a grid shifted by ``-\Delta T/2``, i.e.
    ``T_{\min} - \Delta T/2 + (i-1)\Delta T`` with ``N_T + 2`` points. The two extra
    points keep ``[T_{\min}, T_{\max}]`` strictly bracketed. Sampling ``c_v`` at cell
    midpoints makes it consistent with the slope of the piecewise-linear energy.

Both index pairs are returned together by `get_index_lower_fracpos`; with an
offset table they differ, and mixing them up is silently wrong.

``T(e)`` is inverted by a Newton iteration with tolerance `T_tol`, clamped to
``[1.0001\,T_{\min},\; 0.9999\,T_{\max}]``.

All stored quantities are dimensionless, scaled by the `ReferenceFlowQuantities`
instance `ref_q`, which is retained in the `ref_q` field.

# Arguments
- `ref_q`: `ReferenceFlowQuantities` used to non-dimensionalise every table.
- `mass_arr`: species masses in kg. Not modified.
- `e_c_int_function_arr`: one callable per species, `(m, T) -> (e, c_v)`, returning
    the internal-degree-of-freedom contributions only. The translational parts must
    *not* be included.
- `T_min`, `T_max`, `dT`: tabulation range and step, in K.
- `T_tol`: relative tolerance of the Newton solver for ``T(e)``.
- `interpolation`: only `:linear` is implemented.
- `cv_table_offset`: place ``c_v`` on the midpoint grid, see above.
"""
struct ThermoData1T{I <: Interpolation, CvO <: CvTableOffset, NCOMP} <: ThermoData
    ref_q::ReferenceFlowQuantities

    mass::SVector{NCOMP, Float64} # molecular mass of each component
    inv_mass::SVector{NCOMP, Float64} # inverse of molecular mass of each component

    T_min_E::Float64
    T_max_E::Float64

    T_min_c_v::Float64
    T_max_c_v::Float64

    dT::Float64
    inv_dT::Float64
    T_tol::Float64

    # N_cv_discretization is either N_T_discretization (NoCvOffset)
    # or N_T_discretization+2 (CvOffset)
    e_arr::Array{Float64, 2} # Species-specific tabulated energy, N_T_discretization*NCOMP
    c_v_arr::Array{Float64, 2} # Species-specific tabulated specific heat, N_cv_discretization*NCOMP or
    R_specific::SVector{NCOMP, Float64}  #  k_B / mass [J / kg / K] when not scaled ; component->R_specific

    e_min_arr::SVector{NCOMP, Float64}

    # temperatures at which e(T) is tabulated, T_min_E + i * dT
    T_arr::Vector{Float64}

    # temperatures at which c_v(T) is tabulated, T_min_c_v + i * dT
    # for NoCvOffset this aliases T_arr
    # as well as inverse of the array
    T_c_arr::Vector{Float64}
    T_c_arr_inv::Vector{Float64}

    # used to estimate \int c_v(tau) / tau d tau
    int_c_v_over_t_arr::Array{Float64, 2} # Species-specific tabulated integral part of entropy, N_cv_discretization*NCOMP

    # construct a `ThermoData1T` instance for a flow with NCOMP species
    # using a ReferenceFlowQuantities instance for scaling
    # `mass_arr` is the array of the species' molecular/atomic masses in kg
    # `e_c_int_function_arr` is an array of NCOMP functions
    # each of which computes the specific internal energy and specific heat
    # given a value of the species' mass and the temperature
    # the energy and specific heats of the translational degrees of freedom are
    # account for inside the code and should not be part of the computations in
    # e_c_int_function_arr
    function ThermoData1T{LinearInterpolation, NoCvOffset, NCOMP}(ref_q, mass_arr,
                                                                  e_c_int_function_arr;
                                                                  T_min = 10.0,
                                                                  T_max = 3.0e4,
                                                                  T_tol = 1e-9,
                                                                  dT = 1.0) where {NCOMP}
        @assert (length(mass_arr) == length(e_c_int_function_arr) == NCOMP)

        n_T = trunc(Int, (T_max - T_min) / dT) + 1
        T_arr = Vector(LinRange(T_min, T_max, n_T))
        @assert abs((T_arr[2] - T_arr[1]) - dT) < 1e-3

        # e and c_v share the grid here, so a single sweep fills both tables
        e_arr, c_v_arr = tabulate_e_c(mass_arr, e_c_int_function_arr, T_arr, NCOMP)
        e_min_arr = vec(minimum(e_arr, dims = 1)) ./ ref_q.e_ref
        int_c_v_over_t_arr = tabulate_int_c_v_over_t(c_v_arr, T_arr, dT, NCOMP)

        # k_B / m, computed from the masses in kg, i.e. before they are rescaled below
        R_specific = (k_B ./ mass_arr) ./ ref_q.c_v_ref

        T_min /= ref_q.T_ref
        T_max /= ref_q.T_ref
        dT /= ref_q.T_ref
        T_arr ./= ref_q.T_ref
        T_arr_inv = 1.0 ./ T_arr

        e_arr ./= ref_q.e_ref
        c_v_arr ./= ref_q.c_v_ref
        int_c_v_over_t_arr ./= ref_q.c_v_ref
        mass_arr = mass_arr ./ ref_q.m_ref

        return new(ref_q,
                   mass_arr,
                   1.0 ./ mass_arr,
                   T_min, T_max,
                   T_min, T_max,
                   dT, 1.0 / dT, T_tol,
                   e_arr, c_v_arr,
                   R_specific,
                   e_min_arr,
                   T_arr,
                   T_arr, T_arr_inv,  # c_v tabulated on the same grid as e
                   int_c_v_over_t_arr)
    end

    # same as above, but c_v (and ∫ c_v / T dT) are tabulated on a grid shifted by -dT/2
    # w.r.t. the energy grid: T_c_arr[i] = T_min - dT/2 + (i - 1) * dT, i = 1 ... n_T + 2
    # the two extra points guarantee that [T_min, T_max] is strictly bracketed by the c_v grid
    function ThermoData1T{LinearInterpolation, CvOffset, NCOMP}(ref_q, mass_arr,
                                                                e_c_int_function_arr;
                                                                T_min = 10.0,
                                                                T_max = 3.0e4,
                                                                T_tol = 1e-9,
                                                                dT = 1.0) where {NCOMP}
        @assert (length(mass_arr) == length(e_c_int_function_arr) == NCOMP)
        # the c_v grid starts at T_min - dT/2, which has to stay positive for ∫ c_v / T dT
        @assert T_min > 0.5 * dT

        n_T = trunc(Int, (T_max - T_min) / dT) + 1
        T_arr = Vector(LinRange(T_min, T_max, n_T))
        @assert abs((T_arr[2] - T_arr[1]) - dT) < 1e-3

        n_c = n_T + 2
        T_c_min = T_min - 0.5 * dT
        T_c_max = T_c_min + (n_c - 1) * dT
        T_c_arr = Vector(LinRange(T_c_min, T_c_max, n_c))

        # the two grids differ, so e and c_v need a sweep each
        e_arr, _ = tabulate_e_c(mass_arr, e_c_int_function_arr, T_arr, NCOMP)
        _, c_v_arr = tabulate_e_c(mass_arr, e_c_int_function_arr, T_c_arr, NCOMP)
        e_min_arr = vec(minimum(e_arr, dims = 1)) ./ ref_q.e_ref
        int_c_v_over_t_arr = tabulate_int_c_v_over_t(c_v_arr, T_c_arr, dT, NCOMP)

        # k_B / m, computed from the masses in kg, i.e. before they are rescaled below
        R_specific = (k_B ./ mass_arr) ./ ref_q.c_v_ref

        T_min /= ref_q.T_ref
        T_max /= ref_q.T_ref
        T_c_min /= ref_q.T_ref
        T_c_max /= ref_q.T_ref
        dT /= ref_q.T_ref
        T_arr ./= ref_q.T_ref
        T_c_arr ./= ref_q.T_ref

        e_arr ./= ref_q.e_ref
        c_v_arr ./= ref_q.c_v_ref
        int_c_v_over_t_arr ./= ref_q.c_v_ref
        mass_arr = mass_arr ./ ref_q.m_ref

        return new(ref_q,  # reference quantities used for scaling
                   mass_arr,  # species' masses
                   1.0 ./ mass_arr,  # inverses of the species' masses
                   T_min, T_max,  # minimum and maximum temperatures for the specific energy tables
                   T_c_min, T_c_max,  # minimum and maximum temperatures for the specific heat tables
                   dT, 1.0 / dT, T_tol,  # scaled temperature step size, inverse, tolerance for Newton loop
                   e_arr, c_v_arr,  # scaled arrays of internal energies and specific heats
                   R_specific,  # scaled array of R_specific (gas constants)
                   e_min_arr,  # per-species minimum internal energies
                   T_arr,  # scaled array of temperatures at which e is tabulated
                   T_c_arr, 1.0 ./ T_c_arr,  # scaled array of temperatures and their inverses for the specific heat tables
                   int_c_v_over_t_arr)  # integral of c_v(T)/T used for computation of entropy
    end

    function ThermoData1T(ref_q::ReferenceFlowQuantities, mass_arr,
                          e_c_int_function_arr;
                          T_min = 10.0, T_max = 3.0e4, T_tol = 1e-9, dT = 1.0,
                          interpolation = :linear, cv_table_offset = false)
        NCOMP = length(mass_arr)
        if interpolation == :linear
            if cv_table_offset == true
                return ThermoData1T{LinearInterpolation, CvOffset, NCOMP}(ref_q,
                                                                          mass_arr,
                                                                          e_c_int_function_arr;
                                                                          T_min = T_min,
                                                                          T_max = T_max,
                                                                          T_tol = T_tol,
                                                                          dT = dT)
            else
                return ThermoData1T{LinearInterpolation, NoCvOffset, NCOMP}(ref_q,
                                                                            mass_arr,
                                                                            e_c_int_function_arr;
                                                                            T_min = T_min,
                                                                            T_max = T_max,
                                                                            T_tol = T_tol,
                                                                            dT = dT)
            end
        else
            error("Non-linear interpolation not implemented")
        end
    end
end

@inline function ncomponents(thermodata::ThermoData1T{I, CvO, NCOMP}) where {I, CvO,
                                                                             NCOMP}
    NCOMP
end

@inline function eachcomponent(thermodata::ThermoData1T)
    Base.OneTo(ncomponents(thermodata))
end

# return index and fractional position for energy and cv interpolation in case of no offset
@inline function get_index_lower_fracpos(T,
                                         thermodata::ThermoData1T{I, NoCvOffset, NCOMP}) where {
                                                                                                I,
                                                                                                NCOMP
                                                                                                }
    fracpos = (T - thermodata.T_min_E) * thermodata.inv_dT
    index_lower = floor(Int, fracpos)
    fracpos -= index_lower
    index_lower += 1

    return index_lower, fracpos, index_lower, fracpos
end

# return index and fractional position for energy and cv interpolation in case the
# c_v table is offset by -dT/2 w.r.t. the energy table
# the position on the c_v grid is (T - (T_min_E - dT/2)) / dT = fracpos_global + 1/2,
# so it only differs from the energy one by a constant shift of 1/2 and can be obtained
# from `fracpos` with a single comparison - no second division/floor needed
@inline function get_index_lower_fracpos(T,
                                         thermodata::ThermoData1T{I, CvOffset, NCOMP}) where {
                                                                                              I,
                                                                                              NCOMP
                                                                                              }
    fracpos = (T - thermodata.T_min_E) * thermodata.inv_dT
    index_lower = floor(Int, fracpos)
    fracpos -= index_lower
    index_lower += 1

    # fracpos + 1/2 lies in [1/2, 3/2), so its integer part is exactly `shift`
    shift = fracpos >= 0.5
    index_lower_c = index_lower + shift
    fracpos_c = fracpos + 0.5 - shift

    return index_lower, fracpos, index_lower_c, fracpos_c
end

# compute energy of species i_comp using linear interpolation
@inline function energy_component(i_comp, index_lower_e, fracpos_e,
                                  thermodata::ThermoData1T{LinearInterpolation, CvO,
                                                           NCOMP}) where {CvO, NCOMP}
    @inbounds return thermodata.e_arr[index_lower_e, i_comp] * (1.0 - fracpos_e) +
                     fracpos_e * thermodata.e_arr[index_lower_e + 1, i_comp]
end

# compute energy given temperature and array of densities
# used for prim2cons transformation
@inline function energy_from_rho_vec(rho_vec::SVector, rho, T,
                                     thermodata::ThermoData1T{LinearInterpolation, CvO,
                                                              NCOMP}) where {CvO, NCOMP}
    mixture_energy = 0.0

    index_lower_e, fracpos_e, _, _ = get_index_lower_fracpos(T, thermodata)
    @inbounds for i in eachcomponent(thermodata)
        mixture_energy += energy_component(i, index_lower_e, fracpos_e, thermodata) *
                          rho_vec[i]
    end

    return mixture_energy / rho
end

@inline function c_v_component(i_comp, index_lower_c, fracpos_c,
                               thermodata::ThermoData1T{LinearInterpolation, CvO,
                                                        NCOMP}) where {CvO, NCOMP}
    @inbounds return thermodata.c_v_arr[index_lower_c, i_comp] * (1.0 - fracpos_c) +
                     fracpos_c * thermodata.c_v_arr[index_lower_c + 1, i_comp]
end

# compute specific energy of flow given values of interpolation point and fractional position 
# rho_inv = 1/rho
@inline function energy(u, rho_inv, index_lower_e, fracpos_e,
                        thermodata::ThermoData1T{I, CvO, NCOMP}) where {I, CvO, NCOMP}
    mixture_energy = 0.0
    @inbounds for i in eachcomponent(thermodata)
        mixture_energy += energy_component(i, index_lower_e, fracpos_e, thermodata) *
                          u[i + 3]
    end
    return mixture_energy * rho_inv
end

# compute specific heat capacity of flow given values of interpolation point and fractional position 
# rho_inv = 1/rho
@inline function c_v(u, rho_inv, index_lower_c, fracpos_c,
                     thermodata::ThermoData1T{I, CvO, NCOMP}) where {I, CvO, NCOMP}
    mixture_c_v = 0.0
    @inbounds for i in eachcomponent(thermodata)
        mixture_c_v += c_v_component(i, index_lower_c, fracpos_c, thermodata) * u[i + 3]
    end
    return mixture_c_v * rho_inv
end

# compute ∫ c_v / T dT for a single component using linear interpolation
# ∫ c_v / T dT = ∫_T0^T_a c_v / T dT + ∫_T_a^T_b c_v / T dT
# where T_b is the current temperature, T_a is the closest tabulated temperature s.t. T_a < T_b
# ∫_T0^T_a c_v / T dT - via look-up table
# c_v(T) in interval is linear: c_v(T) = c_v_a + (c_v_n - c_v_a) * (T - T_a) / dT
# c_v_n - c_v(T_a + dT) - next tabulated value
@inline function entropy_c_v_integral_component(i_comp, index_lower_c, T_b,
                                                thermodata::ThermoData1T{LinearInterpolation,
                                                                         CvO, NCOMP}) where {
                                                                                             CvO,
                                                                                             NCOMP
                                                                                             }
    @inbounds T_a = thermodata.T_c_arr[index_lower_c]
    @inbounds T_a_inv = thermodata.T_c_arr_inv[index_lower_c]

    @inbounds c_v_a = thermodata.c_v_arr[index_lower_c, i_comp]    # value of c_v at closest_T
    @inbounds slope = (thermodata.c_v_arr[index_lower_c + 1, i_comp] - c_v_a) *
                      thermodata.inv_dT
    integrate_part = (c_v_a - slope * T_a) * log(T_b * T_a_inv) + slope * (T_b - T_a)

    @inbounds return thermodata.int_c_v_over_t_arr[index_lower_c, i_comp] +
                     integrate_part
end

# compute ∫ c_v / T dT of flow
@inline function entropy_c_v_integral(u, T, rho, thermodata::ThermoData1T)
    mixture_c_v_integral = 0.0

    _, _, index_lower_c, _ = get_index_lower_fracpos(T, thermodata)
    @inbounds for i in eachcomponent(thermodata)
        mixture_c_v_integral += entropy_c_v_integral_component(i, index_lower_c, T,
                                                               thermodata) *
                                u[i + 3]
    end
    return mixture_c_v_integral / rho
end

 @inline function entropy_c_v_integral_taylor_component(i_comp, index_lower_c, T_b,
                                                        thermodata::ThermoData1T{LinearInterpolation,
                                                                                CvO, NCOMP}) where {
                                                                                                    CvO,
                                                                                                    NCOMP
                                                                                                    }
    @inbounds T_a = equations.T_arr[index_lower]
    @inbounds T_a_inv = equations.T_arr_inv[index_lower]

    c_v_b = c_v(i, index_lower, fracpos, equations)  # value of c_v at T
    @inbounds c_v_a = equations.c_v_arr[index_lower, i]  # value of c_v at closest_T
    # (T_b - T_a)/T_a
    Δx = T_b * T_a_inv - 1  # (x-1)
    integrate_part = (c_v_a - (c_v_b - c_v_a) * T_a * equations.inv_ΔT) * (Δx - 0.5 * Δx^2 + Δx^3 / 3) + (c_v_b - c_v_a)
    
    @inbounds return equations.int_c_v_over_t_arr[index_lower, i] + integrate_part
end

# compute ∫ c_v / T dT of flow
@inline function entropy_c_v_taylor(u, T, rho, thermodata::ThermoData1T)
    mixture_c_v_integral = 0.0

    _, _, index_lower_c, _ = get_index_lower_fracpos(T, thermodata)
    @inbounds for i in eachcomponent(thermodata)
        mixture_c_v_integral += entropy_c_v_integral_taylor_component(i, index_lower_c, T,
                                                                      thermodata) *
                                u[i + 3]
    end
    return mixture_c_v_integral / rho
end

# check if energy is too low, return true if it is
# u is vector of conservative flow variables,
# rho_inv is 1/rho, e is the internal energy per unit mass
@inline function limit_T_low_rho_inv(u, rho_inv, e, thermodata::ThermoData1T)
    e_min = 0.0
    @inbounds for i in eachcomponent(thermodata)
        e_min += thermodata.e_min_arr[i] * u[i + 3]
    end
    e_min *= rho_inv
    if e <= e_min
        return true
    else
        return false
    end
end

# compute T(e) via Newton iteration, clamping T to be in range of [1.0001 * T_min_E, 0.9999 * thermodata.T_max_E]
# and return T, index_lower_e, fracpos_e, index_lower_c, fracpos_c (for interpolation)
# u is vector of conservative flow variables,
# rho_inv is 1/rho, T0 is the initial guess for T, e is the internal energy per unit mass
@inline function temperature_rho_inv_with_index(u, rho_inv, T0, e,
                                                thermodata::ThermoData1T)

    # return min/max energy if T0 is less than the lower/upper bound for the tables
    if (T0 < thermodata.T_min_E)
        return (1.0001 * thermodata.T_min_E,
                get_index_lower_fracpos(1.0001 * thermodata.T_min_E, thermodata)...)
    elseif (T0 > thermodata.T_max_E)
        return (0.9999 * thermodata.T_max_E,
                get_index_lower_fracpos(0.9999 * thermodata.T_max_E, thermodata)...)
    end

    # return min temperature if the actual internal energy is less than the lower bound for the tables
    if limit_T_low_rho_inv(u, rho_inv, e, thermodata)
        return (1.0001 * thermodata.T_min_E,
                get_index_lower_fracpos(1.0001 * thermodata.T_min_E, thermodata)...)
    end

    T = T0
    index_lower_e, fracpos_e, index_lower_c, fracpos_c = get_index_lower_fracpos(T,
                                                                                 thermodata)
    fx = energy(u, rho_inv, index_lower_e, fracpos_e, thermodata) - e

    mintol = thermodata.T_tol * e + thermodata.T_tol

    while abs(fx) > mintol
        T -= fx / c_v(u, rho_inv, index_lower_c, fracpos_c, thermodata)
        index_lower_e, fracpos_e, index_lower_c, fracpos_c = get_index_lower_fracpos(T,
                                                                                     thermodata)
        fx = energy(u, rho_inv, index_lower_e, fracpos_e, thermodata) - e
    end
    return T, index_lower_e, fracpos_e, index_lower_c, fracpos_c
end

# compute T(e) via Newton iteration, clamping T to be in range of [1.0001 * T_min_E, 0.9999 * thermodata.T_max_E]
# and return T
# u is vector of conservative flow variables,
# rho_inv is 1/rho, T0 is the initial guess for T, e is the internal energy per unit mass
@inline function temperature_rho_inv(u, rho_inv, T0, e, thermodata::ThermoData1T)
    T, _, _, _, _ = temperature_rho_inv_with_index(u, rho_inv, T0, e, thermodata)

    return T
end

# compute adiabatic index γ(T) via interpolation
# rho_inv = 1/rho
@inline function get_gamma(u, rho_inv, index_lower_c, fracpos_c,
                           thermodata::ThermoData1T)
    c_v_val = c_v(u, rho_inv, index_lower_c, fracpos_c, thermodata)
    c_p = 0.0

    # c_p = c_v + \sum_i rho_i k/m_i/rho =(scaling)= \sum_i rho_i' 1.0/m_i'/rho' (' denotes scaled variables)
    @inbounds for i in eachcomponent(thermodata)
        c_p += u[i + 3] * thermodata.inv_mass[i]
    end

    return (c_v_val + c_p * rho_inv) / c_v_val
end
end # @muladd
